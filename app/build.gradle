plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def getVersionCode = { ->
    try {
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = out
        }
        return Integer.parseInt(out.toString().trim())
    }
    catch (ignored) {
        return 1
    }
}

def getVersionName = { ->
    try {
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = out
        }
        def outStr = out.toString()
        return outStr[1..outStr.length() - 1].trim()
    }
    catch (ignored) {
        return '0.0.0'
    }
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "cn.mfuns.webapp"
        minSdkVersion 25
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 25
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    viewBinding {
        enabled = true
    }
    dataBinding {
        enabled = true
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.google.dagger:hilt-android:2.42'
    kapt 'com.google.dagger:hilt-android-compiler:2.42'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.github.castorflex.smoothprogressbar:library-circular:1.3.0'
    api 'com.tencent.tbs:tbssdk:44181'
    implementation 'com.ilharper:droidup:0.1.5'
}
